plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlinVersion"
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group pluginGroup
version pluginVersion

java {
    sourceCompatibility JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact shadowJar

            afterEvaluate {
                artifactId project.name.toLowerCase()
            }
        }
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        name 'spigot-repo'
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name 'sonatype'
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:$serverVersion"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$kotlinxserializationVersion"

    implementation "org.jetbrains.exposed:exposed-core:$exposedVersion"
    implementation "org.jetbrains.exposed:exposed-dao:$exposedVersion"
    implementation "org.jetbrains.exposed:exposed-jdbc:$exposedVersion"

    implementation "com.google.guava:guava:$guavaVersion"

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testImplementation "org.spigotmc:spigot-api:$serverVersion"
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}
